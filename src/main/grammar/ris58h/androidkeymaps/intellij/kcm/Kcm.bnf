{
  parserClass="ris58h.androidkeymaps.intellij.kcm.parser.KcmParser"

  extends="com.intellij.extapi.psi.ASTWrapperPsiElement"

  psiClassPrefix="Kcm"
  psiImplClassSuffix="Impl"
  psiPackage="ris58h.androidkeymaps.intellij.kcm.psi"
  psiImplPackage="ris58h.androidkeymaps.intellij.kcm.psi.impl"

  elementTypeHolderClass="ris58h.androidkeymaps.intellij.kcm.psi.KcmTypes"
  elementTypeClass="ris58h.androidkeymaps.intellij.kcm.psi.KcmElementType"
  tokenTypeClass="ris58h.androidkeymaps.intellij.kcm.psi.KcmTokenType"
}

kcmFile ::= item*

private item ::= COMMENT|entry
private entry ::= type_declaration|map_entry|key_entry
private recovery_entry ::= !(TYPE_KEYWORD|MAP_KEYWORD|KEY_KEYWORD)

type_declaration ::= TYPE_KEYWORD LABEL {
    pin=1
    recoverWhile="recovery_entry"
}

map_entry ::= MAP_KEYWORD KEY_KEYWORD USAGE_KEYWORD? NUMBER LABEL {
    pin=1
    recoverWhile="recovery_entry"
}

key_entry ::= KEY_KEYWORD (LABEL|NUMBER) key_block {
    pin=1
    recoverWhile="recovery_entry"
}
private key_block ::= LBRACE key_property* RBRACE
key_property ::= PROPERTY_KEY (COMMA PROPERTY_KEY)* SEMICOLON property_value
private property_value ::= CHARACTER_LITERAL|NONE_KEYWORD|(FALLBACK_KEYWORD LABEL)|(REPLACE_KEYWORD LABEL)