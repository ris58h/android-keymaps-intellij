{
  parserClass="ris58h.androidkeymaps.intellij.kl.parser.KlParser"

  extends="com.intellij.extapi.psi.ASTWrapperPsiElement"

  psiClassPrefix="Kl"
  psiImplClassSuffix="Impl"
  psiPackage="ris58h.androidkeymaps.intellij.kl.psi"
  psiImplPackage="ris58h.androidkeymaps.intellij.kl.psi.impl"

  elementTypeHolderClass="ris58h.androidkeymaps.intellij.kl.psi.KlTypes"
  elementTypeClass="ris58h.androidkeymaps.intellij.kl.psi.KlElementType"
  tokenTypeClass="ris58h.androidkeymaps.intellij.kl.psi.KlTokenType"
}

klFile ::= item_*

private item_ ::= (COMMENT|key_expr|axis_expr|led_expr|sensor_expr|rkc_expr)

key_expr ::= KEY_KEYWORD USAGE_KEYWORD? NUMBER LABEL key_flags
key_flags ::= (LABEL)*

axis_expr ::= AXIS_KEYWORD NUMBER (LABEL|axis_split|axis_invert) axis_flat
private axis_split ::= SPLIT_KEYWORD NUMBER LABEL LABEL
private axis_invert ::= INVERT_KEYWORD LABEL
private axis_flat ::= (FLAT_KEYWORD NUMBER?)?

led_expr ::= LED_KEYWORD USAGE_KEYWORD? NUMBER LABEL

sensor_expr ::= SENSOR_KEYWORD NUMBER LABEL LABEL

rkc_expr ::= RKC_KEYWORD LABEL
