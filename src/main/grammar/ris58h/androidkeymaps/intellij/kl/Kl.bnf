{
  parserClass="ris58h.androidkeymaps.intellij.kl.parser.KlParser"

  extends="com.intellij.extapi.psi.ASTWrapperPsiElement"

  psiClassPrefix="Kl"
  psiImplClassSuffix="Impl"
  psiPackage="ris58h.androidkeymaps.intellij.kl.psi"
  psiImplPackage="ris58h.androidkeymaps.intellij.kl.psi.impl"

  elementTypeHolderClass="ris58h.androidkeymaps.intellij.kl.psi.KlTypes"
  elementTypeClass="ris58h.androidkeymaps.intellij.kl.psi.KlElementType"
  tokenTypeClass="ris58h.androidkeymaps.intellij.kl.psi.KlTokenType"
}

klFile ::= item*

private item ::= COMMENT|expr
private expr ::= key_expr|axis_expr|led_expr|sensor_expr|rkc_expr
private recovery_expr ::= !(KEY_KEYWORD|AXIS_KEYWORD|LED_KEYWORD|SENSOR_KEYWORD|RKC_KEYWORD)

key_expr ::= KEY_KEYWORD USAGE_KEYWORD? NUMBER LABEL key_flags {
   pin=1
   recoverWhile="recovery_expr"
}
key_flags ::= LABEL*

axis_expr ::= AXIS_KEYWORD NUMBER (LABEL|axis_split|axis_invert) axis_flat? {
    pin=1
    recoverWhile="recovery_expr"
}
private axis_split ::= SPLIT_KEYWORD NUMBER LABEL LABEL
private axis_invert ::= INVERT_KEYWORD LABEL
private axis_flat ::= (FLAT_KEYWORD NUMBER?)

led_expr ::= LED_KEYWORD USAGE_KEYWORD? NUMBER LABEL {
    pin=1
    recoverWhile="recovery_expr"
}

sensor_expr ::= SENSOR_KEYWORD NUMBER LABEL LABEL {
    pin=1
    recoverWhile="recovery_expr"
}

rkc_expr ::= RKC_KEYWORD LABEL {
    pin=1
    recoverWhile="recovery_expr"
}
